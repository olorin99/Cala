#version 450

layout (local_size_x = 16) in;

struct CameraData {
    mat4 projection;
    mat4 view;
    vec3 position;
    float near;
    float far;
};

layout (set = 0, binding = 0) uniform FrameData {
    CameraData camera;
    vec4 planes[6];
};

layout (set = 1, binding = 0) readonly buffer ModelData {
    mat4 inModel[];
};

layout (set = 1, binding = 1) writeonly buffer RenderList {
    uint modelIndex[];
};

struct InstanceData {
    vec3 position;
    float scale;
};

layout (set = 1, binding = 2) readonly buffer PositionList {
    InstanceData instances[];
};

layout (set = 2, binding = 0) buffer Output {
    uint drawCount;
};

bool frustumCheck(vec3 pos, float radius) {
    for (int i = 0; i < 6; i++) {
        if (dot(vec4(pos, 1.0), planes[i]) + radius < 0.0) {
            return false;
        }
    }
    return true;
}

void main() {
    uint idx = gl_GlobalInvocationID.x + gl_GlobalInvocationID.y * gl_NumWorkGroups.x * gl_WorkGroupSize.x;

    if (idx == 0) {
        atomicExchange(drawCount, 0);
    }

    vec3 point = instances[idx].position;

    if (frustumCheck(point, 1.0)) {
        uint a = atomicAdd(drawCount, 1);
        modelIndex[a] = idx;
    }
}
