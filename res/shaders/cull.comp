#version 450

layout (local_size_x = 16) in;

struct CameraData {
    mat4 projection;
    mat4 view;
    vec3 position;
    float near;
    float far;
};

layout (set = 1, binding = 0) uniform FrameData {
    CameraData camera;
    vec4 planes[6];
};

layout (set = 2, binding = 0) readonly buffer ModelData {
    mat4 transforms[];
};

struct Mesh {
    uint firstIndex;
    uint indexCount;
    uint materialIndex;
};

layout (set = 2, binding = 1) readonly buffer MeshData {
    Mesh meshData[];
};

struct IndexedIndirectCommand {
    uint indexCount;
    uint instanceCount;
    uint firstIndex;
    uint vertexOffset;
    uint firstInstance;
};

layout (set = 2, binding = 2) writeonly buffer DrawCommands {
    IndexedIndirectCommand commands[];
};

layout (set = 2, binding = 3) buffer Output {
    uint drawCount;
};

bool frustumCheck(vec3 pos, float radius) {
    for (int i = 0; i < 6; i++) {
        if (dot(vec4(pos, 1.0), planes[i]) + radius < 0.0) {
            return false;
        }
    }
    return true;
}

void main() {
    uint idx = gl_GlobalInvocationID.x + gl_GlobalInvocationID.y * gl_NumWorkGroups.x * gl_WorkGroupSize.x;

    if (idx == 0) {
        atomicExchange(drawCount, 0);
    }

    vec3 point = transforms[idx][2].xyz;

    if (frustumCheck(point, 1.0)) {
        uint a = atomicAdd(drawCount, 1);
        Mesh mesh = meshData[idx];
        IndexedIndirectCommand command;
        command.indexCount = mesh.indexCount;
        command.firstIndex = mesh.firstIndex;
        command.vertexOffset = 0;
        command.firstInstance = idx;
        command.instanceCount = 1;
        commands[a] = command;
    }
}
