
layout (local_size_x = 64) in;
layout (triangles, max_vertices = 64, max_primitives = 64) out;

layout (location = 0) out VsOut {
        vec3 FragPos;
        vec2 TexCoords;
        mat3 TBN;
        vec3 ViewPos;
        flat uint drawID;
        flat uint meshletID;
} meshOut[];

#include "shaderBridge.h"

struct TaskPayload {
    uint meshIndex;
    uint offset[64];
};
taskPayloadSharedEXT TaskPayload payload;

void main() {

    uint threadIndex = gl_LocalInvocationIndex;

    GPUCamera camera = globalData.cameraBuffer[globalData.primaryCameraIndex].camera;
    mat4 model = globalData.transformsBuffer.transforms[payload.meshIndex];

    GPUMesh mesh = globalData.meshBuffer.meshData[payload.meshIndex];

    uint meshletIndex = mesh.meshletIndex + payload.offset[gl_WorkGroupID.x];
    Meshlet meshlet = globalData.meshletBuffer.meshlets[meshletIndex];

    SetMeshOutputsEXT(meshlet.indexCount, meshlet.primitiveCount);

    if (threadIndex < meshlet.indexCount) {
        uint index = globalData.meshletIndexBuffer.indices[meshlet.indexOffset + threadIndex] + meshlet.vertexOffset;
        Vertex vertex = globalData.vertexBuffer.vertices[index];
        vec4 fragPos = model * vec4(vertex.position, 1.0);

        float tangentDirection = vertex.tangent.w;
        if (tangentDirection == 0)
            tangentDirection = 1;

        vec3 T = normalize(mat3(model) * vertex.tangent.xyz) * tangentDirection;
        vec3 N = normalize(mat3(model) * vertex.normal);
        T = normalize(T - dot(T, N) * N);
        vec3 B = cross(N, T);

        meshOut[threadIndex].FragPos = fragPos.xyz;
        meshOut[threadIndex].TexCoords = vertex.texCoords;
        meshOut[threadIndex].TBN = mat3(T, B, N);
        meshOut[threadIndex].ViewPos = camera.position;
        meshOut[threadIndex].drawID = payload.meshIndex;
        meshOut[threadIndex].meshletID = meshletIndex;

        gl_MeshVerticesEXT[threadIndex].gl_Position = camera.projection * camera.view * fragPos;
    }

    if (threadIndex < meshlet.primitiveCount) {
        uint a = globalData.primitiveBuffer.primitives[meshlet.primitiveOffset + threadIndex * 3 + 0];
        uint b = globalData.primitiveBuffer.primitives[meshlet.primitiveOffset + threadIndex * 3 + 1];
        uint c = globalData.primitiveBuffer.primitives[meshlet.primitiveOffset + threadIndex * 3 + 2];

        gl_PrimitiveTriangleIndicesEXT[threadIndex] = uvec3(a, b, c);
    }
}