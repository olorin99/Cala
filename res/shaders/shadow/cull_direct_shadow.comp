
layout (local_size_x = LOCAL_SIZE_X) in;

#include "shaderBridge.h"

struct IndexedIndirectCommand {
    uint indexCount;
    uint instanceCount;
    uint firstIndex;
    uint vertexOffset;
    uint firstInstance;
};

layout (scalar, set = 2, binding = 0) writeonly buffer DrawCommands {
    MeshTaskCommand commands[];
};

layout (push_constant) uniform FrameData {
    uint cameraIndex;
};

layout (set = 2, binding = 1) buffer Output {
    uint drawCount;
};

bool frustumCheck(vec3 pos, float radius) {
    GPUCamera cullingCamera = globalData.cameraBuffer[cameraIndex].camera;

    for (int i = 0; i < 6; i++) {
        if (dot(vec4(pos, 1.0), cullingCamera.frustum.planes[i]) + radius < 0.0) {
            return false;
        }
    }
    return true;
}

uint getLOD(uint lodCount, vec3 aabbMin, vec3 aabbMax) {
    GPUCamera camera = globalData.cameraBuffer[cameraIndex].camera;
    vec3 pos = camera.position;

    float dx = max(aabbMin.x - pos.x, max(0.0, pos.x - aabbMax.x));
    float dy = max(aabbMin.y - pos.y, max(0.0, pos.y - aabbMax.y));
    float dz = max(aabbMin.z - pos.z, max(0.0, pos.z - aabbMax.z));
    float dist = sqrt(dx * dx + dy * dy + dz + dz);


    const float firstLODDist = min(camera.far, 1000) * 0.3;
    const float maxDist = min(camera.far, 1000) - firstLODDist;
    const float restLODDist = maxDist / MAX_LODS;

    const uint maxLods = min(lodCount, MAX_LODS);

    for (uint i = 0; i < maxLods; i++) {
        if (dist < (firstLODDist + restLODDist * i)) {
            return i;
        }
    }
    return maxLods - 1;
}

void main() {
    uint idx = gl_GlobalInvocationID.x + gl_GlobalInvocationID.y * gl_NumWorkGroups.x * gl_WorkGroupSize.x;
    if (idx >= globalData.maxDrawCount)
    return;

    if (idx == 0) {
        atomicExchange(drawCount, 0);
    }

    barrier();

    GPUMesh mesh = globalData.meshBuffer.meshData[idx];
    if (mesh.enabled == 0 || mesh.castShadows == 0)
        return;

    vec3 center = (mesh.max.xyz + mesh.min.xyz) * 0.5;
    center = (globalData.transformsBuffer.transforms[idx] * vec4(center, 1.0)).xyz;
    vec3 halfExtent = (mesh.max.xyz - mesh.min.xyz) * 0.5;

    bool visible = true;
    if (globalData.gpuCulling > 0) {
        visible = frustumCheck(center, length(halfExtent));
    }
    if (visible) {
        uint lod = getLOD(mesh.lodCount, mesh.min.xyz, mesh.max.xyz);
        uint meshletCount = mesh.lods[lod].meshletCount;

        uint a = atomicAdd(drawCount, 1);

        MeshTaskCommand command;
        command.x = uint(ceil(meshletCount / 64.0));
        command.y = 1;
        command.z = 1;
        command.meshID = idx;
        command.meshLOD = lod;
        commands[a] = command;
    }
}
