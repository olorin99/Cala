#version 450

layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

struct CameraData {
    mat4 projection;
    mat4 view;
    vec3 position;
    float near;
    float far;
    float exposure;
};

layout (set = 1, binding = 0) uniform FrameData {
    CameraData camera;
};

struct GlobalData {
    float gamma;
    uint time;
};

layout (set = 1, binding = 1) uniform Global {
    GlobalData globalData;
};

layout (rgba32f, set = 2, binding = 0) uniform readonly image2D hdrMap;
layout (set = 2, binding = 1) uniform writeonly image2D backbuffer;

vec3 uncharted2Tonemap(vec3 x) {
    float A = 0.15;
    float B = 0.50;
    float C = 0.10;
    float D = 0.20;
    float E = 0.02;
    float F = 0.30;
    return ((x*(A*x+C*B)+D*E)/(x*(A*x+B)+D*F))-E/F;
}


void main() {

    ivec2 globCoords = ivec2(gl_GlobalInvocationID.xy);
    vec2 texCoords = globCoords / vec2(1920.0, 1080.0);

    vec3 hdr = imageLoad(hdrMap, globCoords).rgb;

//    vec3 result = vec3(1.0) - exp(-hdr * camera.exposure);

    vec3 result = uncharted2Tonemap(hdr * camera.exposure);
    result = result * (1.0 / uncharted2Tonemap(vec3(11.2f)));

    result = pow(result, vec3(1.0 / globalData.gamma));

    imageStore(backbuffer, globCoords, vec4(result, 1.0));

}
