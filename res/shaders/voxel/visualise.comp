
layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

layout (rgba32f, set = 0, binding = 3) uniform readonly image3D voxelGrid[];

#include "camera.glsl"

#include "global_data.glsl"

layout (push_constant) uniform PushData {
    mat4 voxelOrthographic;
    int voxelGridIndex;
};

layout (set = 2, binding = 0) uniform writeonly image2D backbuffer;

vec3 scaleAndBias(vec3 p) {
    return vec3(p.xy * 0.5 + 0.5, p.z);
}

bool isInsideCube(vec3 p, float e) {
    return p.x < 1 + e && p.y < 1 + e && p.z < 1 + e && p.x > 0 - e && p.y > 0 - e && p.z > 0 - e;
}

void main() {
    ivec2 globCoords = ivec2(gl_GlobalInvocationID.xy);
    vec2 texCoords = vec2(globCoords) / vec2(imageSize(backbuffer));

    CameraData camera = bindlessBuffersCamera[globalData.cameraBufferIndex].camera;

    vec3 origin = camera.position;

    vec4 p = inverse(camera.projection * camera.view) * vec4(texCoords * 2 - 1, 0, 1);
    p /= p.w;

    vec3 direction = normalize(p.xyz - origin);
//    vec4 colour = vec4(texCoords, 0.0, 1.0);

    vec4 colour = vec4(0.0);

    for (int step = 1; step < 1000 && colour.a < 0.99f; step++) {
        vec3 currentPos = origin + direction * step * 0.1;
        vec4 i = voxelOrthographic * vec4(currentPos, 1.0);
        vec3 voxelPos = scaleAndBias(i.xyz);
        if (!isInsideCube(voxelPos, 0))
            continue;

        ivec3 dim = imageSize(voxelGrid[voxelGridIndex]);
        ivec3 voxelCoords = ivec3(dim * voxelPos);
//
//        voxelCoords = min(voxelCoords, dim);
//        voxelCoords = max(voxelCoords, ivec3(0));

        vec4 c = imageLoad(voxelGrid[voxelGridIndex], voxelCoords);
//        colour += (1.0f - colour.a) * vec4(voxelPos, 1.0);
        colour += (1.0f - colour.a) * c;
    }


    imageStore(backbuffer, globCoords, colour);
}
